/*******************************************************************************
 * Copyright 2017-2023 Open Text.
 *
 * The only warranties for products and services of Open Text and
 * its affiliates and licensors ("Open Text") are as may be set forth
 * in the express warranty statements accompanying such products and services.
 * Nothing herein should be construed as constituting an additional warranty.
 * Open Text shall not be liable for technical or editorial errors or
 * omissions contained herein. The information contained herein is subject
 * to change without notice.
 *
 * Except as specifically indicated otherwise, this document contains
 * confidential information and a valid license is required for possession,
 * use or copying. If this work is provided to the U.S. Government,
 * consistent with FAR 12.211 and 12.212, Commercial Computer Software,
 * Computer Software Documentation, and Technical Data for Commercial Items are
 * licensed to the U.S. Government under vendor's standard commercial license.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *   http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

plugins {
    id 'java'
    id 'org.jetbrains.intellij.platform' version '2.6.0'
}

project.group = projectGroup
project.version = projectVersion

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://nexus.octane.admlabs.aws.swinfra.net/nexus/content/repositories/mqm-all'
        allowInsecureProtocol = true
    }
    intellijPlatform {
        defaultRepositories()
    }
}

clean.doLast {
    file('.sandbox').deleteDir()
    file('.intellijPlatform').deleteDir()
}

sourceSets {
    main {
        java.srcDir 'src/main/java'
        resources.srcDir 'src/main/resources'
    }
    test {
        java.srcDir 'src/test/java'
        resources.srcDir 'src/test/resources'
    }
}

dependencies {
    intellijPlatform {
        def type = providers.gradleProperty('ideaType')
        def version = providers.gradleProperty('ideaVersion')

        create(type, version)

        bundledPlugin 'com.intellij.java'
    }

    implementation("com.hpe.adm.octane.ideplugins:octane-plugin-common:$commonVersion") {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    def javaFxVersion = '17.0.16'
    def javaFxModules = ['graphics', 'swing', 'base', 'web', 'controls', 'media']
    def platforms = ['linux', 'win', 'mac']

    platforms.each { platform ->
        javaFxModules.each { module ->
            implementation "org.openjfx:javafx-${module}:${javaFxVersion}:${platform}"
        }
    }

    implementation group: 'org.jsoup', name: 'jsoup', version: '1.8.3'

    implementation('com.google.inject:guice:4.0') {
        exclude group: 'com.google.guava', module: 'guava'
    }

    implementation('com.google.inject.extensions:guice-multibindings:4.0') {
        exclude group: 'com.google.guava', module: 'guava'
    }

    testImplementation group: 'junit', name: 'junit', version: '4.11'
}

intellijPlatform {
    projectName = projectPluginName
}

tasks.withType(JavaExec) {
    jvmArgs '--add-exports', 'java.base/jdk.internal.vm=ALL-UNNAMED'
    if (System.getProperty('DEBUG', 'false') == 'true') {
        jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=9099'
    }
}

tasks.processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task copyDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into project.rootDir.canonicalPath + '/build/stat'
}

configurations.all {
    exclude module: 'logback-classic-1.2.3'
}